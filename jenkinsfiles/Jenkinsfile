properties([
    parameters([
        [$class: 'DynamicReferenceParameter',
            omitValueField: true,
            choiceType: 'ET_FORMATTED_HTML',
            description: 'Define context path',
            name: 'CONTEXT_PATH',
            referencedParameters: 'DOCKERFILE_TO_BUILD,CONTEXT_REQUIRED',
            script: [
                $class: 'GroovyScript',
                fallbackScript: [
                    classpath: [],
                    sandbox: false,
                    script:
                        "return 'Could not get the services list'"
                ],
                script: [
                    classpath: [],
                    sandbox: false,
                    script:
                    // Remove whitespaces and hidden chars
                    '''
                    if (CONTEXT_REQUIRED) {
                        return "<input name='value' type='text' value='${DOCKERFILE_TO_BUILD}/' class='setting-input'>"
                    } else {
                        return "<p>Field disabled</p>"
                    }
                    '''.stripIndent().trim()
                ]
            ]
        ]
    ])
])

pipeline {
    agent {
        kubernetes {
            inheritFrom 'default'
            yaml '''
            spec:
              containers:
                - name: kaniko
                  image: gcr.io/kaniko-project/executor:debug
                  command:
                   - "sleep"
                  args:
                    - "999999"
                  securityContext:
                    privileged: true
                    runAsUser: 1000
                    fsGroup: 1000
                    capabilities:
                      add:
                        - "CHOWN"
                        - "DAC_OVERRIDE"
                        - "FOWNER"
                        - "SETFCAP"
                        - "SETGID"
                        - "SETUID"
                  volumeMounts:
                    - name: registry-credentials
                      mountPath: /kaniko/.docker
              restartPolicy: Never
              volumes:
                - name: registry-credentials
                  secret:
                    secretName: nexus3-k8s-lan-credentials
                    items:
                      - key: config.json
                        path: config.json
            '''
            defaultContainer 'kaniko'
        }
    }
    options {
        buildDiscarder(
            logRotator(numToKeepStr: '5')
        )
        timestamps()
        disableConcurrentBuilds()
        timeout(
            time: 1,
            unit: 'HOURS'
        )
        durabilityHint('PERFORMANCE_OPTIMIZED')
        ansiColor('xterm')
    }
    environment {
        PRIVATE_REGISTRY_ADDRESS = 'nexus3.k8s.lan:50000'
    }
    parameters {
        choice(
            choices: [
                'python-webdev',
                'jenkins-customagent',
                'redhat8-ansible',
                'ubuntu-ansible'
            ],
            description: 'Select Dockerfile to build',
            name: 'DOCKERFILE_TO_BUILD'
        )
        string(
            defaultValue: 'myimage:example',
            description: 'Name of output image in format image:tag',
            name: 'IMAGE_NAME_AND_TAG',
            trim: true
        )
        booleanParam(
            description: 'Is context required to build image?',
            name: 'CONTEXT_REQUIRED'
        )
    }
    stages {
        stage('Pull Repository') {
            steps {
                git(
                    credentialsId: 'github-access-token-mrachuta',
                    url: 'https://github.com/mrachuta/dockerfiles.git'
                )
            }
        }
        stage('Build image') {
            steps {
                script {
                    if (!env.CONTEXT_REQUIRED || env.CONTEXT_PATH.length() == 0) {
                        env.CONTEXT_PATH_ARG = ''
                    } else {
                        env.CONTEXT_PATH_ARG = "--context=dir://${env.CONTEXT_PATH}"
                    }
                    sh """
                        /kaniko/executor --dockerfile="${DOCKERFILE_TO_BUILD}/Dockerfile" \
                        --destination="${PRIVATE_REGISTRY_ADDRESS}/${IMAGE_NAME_AND_TAG}" \
                        ${CONTEXT_PATH_ARG} --insecure --insecure-pull
                    """
                }
            }
        }
    }
}
